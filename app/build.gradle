plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'org.jetbrains.dokka'
}

def versionMajor = 1
def versionMinor = 2
def versionPatch = 1

android {
    compileSdk 31

    defaultConfig {
        applicationId "jp.mydns.kokoichi0206.countdowntimer"
        minSdk 26
        targetSdk 31
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    sourceSets {
        main { java.srcDirs = ['src/main/java'] }
        test { java.srcDirs = ['src/test/java'] }
        androidTest { java.srcDirs = ['src/androidTest/java'] }
    }
    signingConfigs {
        release {
            storeFile file('release.keystore') // 指定パスに配置
            storePassword System.getenv('RELEASE_KEYSTORE_STORE_PASSWORD') ?: RELEASE_STORE_PASSWORD
            keyAlias System.getenv('KEY_ALIAS') ?: RELEASE_KEY_ALIAS
            keyPassword System.getenv('RELEASE_KEYSTORE_KEY_PASSWORD') ?: RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.5.21'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dokkaHtml.configure {
    // projectDir は app フォルダのこと
    outputDirectory.set(new File("${projectDir}/build/dokka"))
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
    implementation 'androidx.activity:activity-compose:1.4.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Compose dependencies
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.17.0"

    // Number picker
    implementation "io.github.vanpra.compose-material-dialogs:datetime:0.6.3"

    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // OSS License display
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
}